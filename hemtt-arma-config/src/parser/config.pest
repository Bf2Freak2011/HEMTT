file = { SOI ~ config ~ EOI }
config = { item* }
item = _{ ( ( prop | propexpand | class | classextends | classdef | classdelete | ident ) ~ ";" ) }

prop = { ( ident ~ "=" ~ value | identarray ~ "=" ~ array ) }
propexpand = { identarray ~ "+=" ~ array }

ident = @{ ( ASCII_ALPHANUMERIC | "_" )+ }
value = _{
    bool |
    float |
    integer |
    string_wrapper |
    special |
    unquoted
}

identarray = ${ ident ~ "[]" }

bool = @{ "true" | "false" }
integer = @{ "-"? ~ ASCII_DIGIT+ }
float = @{ "-"? ~ ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT* }
string_wrapper = _{ "\"" ~ string ~ "\"" }
string = @{ ( "\"\"" | (!"\"" ~ ANY) )* }
unquoted = ${ ( "\\\n" | "\\\r\n" | char )+ }
char = { !( "\n" | ";" ) ~ ANY }

special = { "__FILE__" | "__LINE__" }

array = !{ "{" ~ "}" | "{" ~ ( array | value ) ~ ( "," ~ ( array | value ) )* ~ "}" }

class = { "class " ~ ident ~ "{" ~ item+ ~ "}" }
classextends = { "class " ~ ident ~ ":" ~ ident ~ "{" ~ item+ ~ "}" }
classdef = { "class " ~ ident }
classdelete = { "delete " ~ ident }

COMMENT = _{ "//" ~ ( !"\n" ~ ANY )* ~ "\n" | "/*" ~ ( !"*/" ~ ANY )* ~ "*/" }
WHITESPACE = _{ " " | "\t" | "\r" | ( !"\\" ~ "\n" ) }
